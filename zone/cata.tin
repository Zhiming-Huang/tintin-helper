#class cata kill
#class cata open

#var MAXOPPONENTS 0;
#var CATATOTAL 0;

#event {VARIABLE UPDATED MSDP_OPPONENT_LEVEL} {#showme <ccf>Opponent Level: <cff>$MSDP_OPPONENT_LEVEL};

#tick {violence} {
	#if {${MSDP_OPPONENT_NUMBER} > 1} {
		spellAdd combat chain
	};

	#if {${MSDP_OPPONENT_NUMBER} > ${MAXOPPONENTS}} {#var MAXOPPONENTS $MSDP_OPPONENT_NUMBER};
} {4};

event_register {mob kill} {cata} {catacounter {%2}};

#alias catacounter {
	#if {!&catakills[%1]} {#var catakills[%1] 1} {#math {catakills[%1]} {$catakills[%1] + 1}};
	#if {"$autoskin" == "on"} {nco skin corpse};
	#if {@catatotalkills{} > ($CATATOTAL * 1.1)} {
		gt Total kills: @catatotalkills{};
		#var CATATOTAL @catatotalkills{};
	};
}

#action {^You skin some %1 hide from} {
	#switch {"%1"} {
		#case {"rough"} {sac hide};
		#case {"junk"} {sac hide};
		#case {"average"} {sac hide};
		#default {label hide %1;pp hide};
	};
}

#act {An armored warhorse flies in} {spellAdd any {chain warhorse}}
#act {^A battering ram arrives} {spellAdd any {chain ram}}

#tab catastats
#alias catastats {
	#var catakills;
	#showme <cca>Max simultaneous opponents: <ccf>${MAXOPPONENTS};
	#showme <cca>Gold: <ccf>@hNum{$catagold};

	#showme <cca>Total kills: <ccf>@catatotalkills{};
}

#act {and you receive %d.$} {
	#math catagold {$catagold + %1};
}

#act {^The hide won't fit} {give hide bif;oo put all.hide tent};

#function catatotalkills {
	#var result 0;
	#foreach {*catakills[]} {tmp} {
		#math {result} {$result + $catakills[$tmp]};
	};
}

#class cata close
#nop -- vim: syntax=tt
